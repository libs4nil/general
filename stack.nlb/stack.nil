// License Information:
// stack.nlb/stack.nil
// Stack
// version: 19.07.25
// Copyright (C)  Jeroen P. Broks
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information



class TStack
	
	private table values
	private string wantedtype
	
	void CONSTRUCTOR(mytype)
		mytype = mytype or "var"
		switch mytype
			case "string" "var" "number" "table" "function" "boolean"
				self.wantedtype = mytype
			case "delegate"
				self.wantedtype = "function"
			case "int"
				self.wantedtype = "number"
			case "bool"
				self.wantedtype = "bool"
			case "nil"
				error("'nil' is not allowed for stacking!")
			default
				error("Unknown type requested")
		end
	end
	
	get var Value
		var ret
		if #self.values<=0 then return nil end
		ret = self.values[#self.values]
		self.values[#self.values]=nil // popping = removing too!
		return ret
	end
	
	set var Value
		assert(value and (self.wantedtype=="var" or type(value)==self.wantedtype),"Stack wants "..self.wantedtype.."; got "..type(value))
		self.values[#self.values+1] = value
	end
	
	void Push(v)
		self.Value = v
	end
	
	var Pop()
		return self.Value
	end
	
	get var PopEach
		return var()
			return self.Value
		end
	end
	
	get var NoPopEach
		int i
		i=#self.values + 1
		return var()
			i = i - 1
			if i<=0 then return nil end
			return self.values[i]
		end
	end
	
end



